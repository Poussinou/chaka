apply plugin: 'com.android.application'

group = 'net.timelegend.chaka'
version = '1.24.8a.6'

dependencies {
	if (file('../lib/build.gradle').isFile())
		api project(':lib')
	else
		api 'net.timelegend.chaka:viewer:1.24.8a.6'
}

android {
	compileSdkVersion 34
	defaultConfig {
        applicationId 'net.timelegend.chaka.viewer.app'
        namespace 'net.timelegend.chaka.viewer.app'
		minSdkVersion 21
		targetSdkVersion 34
		versionName '1.24.8a.6'
		versionCode 153
	}
	splits {
		abi {
			enable true
			universalApk true
		}
	}
	bundle {
		abi {
			enableSplit true
		}
	}
	if (project.hasProperty('release_storeFile')) {
		signingConfigs {
			release {
				storeFile file(release_storeFile)
				storePassword release_storePassword
				keyAlias release_keyAlias
				keyPassword release_keyPassword
			}
		}
		buildTypes {
			release {
				signingConfig signingConfigs.release
				ndk {
					debugSymbolLevel 'FULL'
				}
			}
		}
	}
}
